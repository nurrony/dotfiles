#!/usr/bin/env zsh
# setup fzf for zsh

export FZF_DEFAULT_COMMAND="rg --files --hidden"
export FZF_DEFAULT_OPTS="--multi --height 70% --layout=default --border --color=hl:#fbb388"
export FZF_CTRL_T_OPTS="--multi --preview 'bat --color=always -n --line-range :100 {}'"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

export FZF_ALT_C_OPTS="--preview 'eza --tree {} | head -200'"
export FZF_ALT_C_COMMAND="rg --files --hidden --type d"
export FZF_CTRL_R_OPTS="--bind 'ctrl-y:execute-silent(echo -n {2..} | pbcopy)+abort' --color header:italic --header 'Press CTRL-Y to copy command into clipboard'"

# Set up fzf key bindings and fuzzy completion
source <(fzf --zsh)

nmr_fzf_find_edit() {
  [ "$#" -ne 0 ] && nvim "$@" || rg --files --hidden \
    | fzf --print0 --multi --height 50% --layout=default --border --preview 'bat --color=always -n --line-range :100 {}' \
    | xargs -0 -r nvim
}

nmr_fzf_kill() {
    if [[ $(uname) == Linux ]]; then
        local pids=$(ps -f -u $USER | sed 1d | fzf | awk '{print $2}')
    elif [[ $(uname) == Darwin ]]; then
        local pids=$(ps -f -u $USER | sed 1d | fzf | awk '{print $3}')
    else
        echo 'Error: unknown platform'
        return
    fi
    if [[ -n "$pids" ]]; then
        echo "$pids" | xargs kill -9 "$@"
    fi
}

nmr_fzf_git_reflog() {
    local selection=$(
      git reflog --color=always "$@" |
        fzf --no-multi --ansi --no-sort --no-height \
            --preview "git show --color=always {1}"
    )
    if [[ -n $selection ]]; then
        git show $(echo $selection | awk '{print $1}')
    fi
}


nmr_fzf_git_log_pickaxe() {
     if [[ $# == 0 ]]; then
         echo 'Error: search term was not provided.'
         return
     fi
     local selection=$(
       git log --oneline --color=always -S "$@" |
         fzf --no-multi --ansi --no-sort --no-height \
             --preview "git show --color=always {1}"
     )
     if [[ -n $selection ]]; then
         local commit=$(echo "$selection" | awk '{print $1}' | tr -d '\n')
         git show $commit
     fi
 }
